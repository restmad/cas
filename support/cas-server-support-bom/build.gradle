description = "Apereo CAS ‌‌Bill of Material"
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact file("cas-server-support-bom.md")
            pom {
                url = "$projectUrl"
                inceptionYear = project.projectInceptionYear
                name = project.name
                description = project.name
                packaging = "pom"
                issueManagement {
                    system = project.projectIssueSystem
                    url = project.projectIssueUrl
                }
                developers {
                    developer {
                        id = "mmoayyed"
                        name = "Misagh Moayyed"
                    }
                }
            }

            pom.withXml {
                println "Creating BOM with all project dependencies for ${project.name}"

                def dependencyManagement = asNode().appendNode("dependencyManagement")
                def dependencies = dependencyManagement.appendNode("dependencies")


                rootProject.subprojects.findAll { p -> p != project && p.tasks.asMap['generatePomFileForMavenJavaPublication']?.enabled }.each { p ->
                    def dependency = dependencies.appendNode("dependency")
                    dependency.appendNode("groupId", groupId)
                    dependency.appendNode("artifactId", p.name)
                    dependency.appendNode("version", version)
                }

                libraries.each {
                    if (it.value instanceof List) {
                        it.value.each { d ->
                            def dependency = dependencies.appendNode("dependency")
                            dependency.appendNode("groupId", d.group)
                            dependency.appendNode("artifactId", d.name)
                            dependency.appendNode("version", d.version)
                        }
                    } else {
                        def d = it.value
                        def dependency = dependencies.appendNode("dependency")
                        dependency.appendNode("groupId", d.group)
                        dependency.appendNode("artifactId", d.name)
                        dependency.appendNode("version", d.version)
                    }
                }
            }
        }
    }
}
